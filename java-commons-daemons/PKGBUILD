# Maintainer: Guillaume ALAUX <guillaume@archlinux.org>
_libname=commons-daemon
pkgname=("java-${_libname}" 'java-jsvc')
pkgbase=java-${_libname}
pkgver=1.0.5
pkgrel=1
arch=('i686' 'x86_64')
url="http://commons.apache.org/daemon/"
license=('APACHE')
source=(http://archive.apache.org/dist/commons/daemon/source/${_libname}-${pkgver}-src.tar.gz)
md5sums=('2fe0a35d4a9c0338ecd45dad20f33662')
makedepends=('apache-ant' 'docbook2x' 'docbook-xsl')


build() {
  # commons-daemon.jar
  cd "${srcdir}/${_libname}-${pkgver}-src"
  ant

  # jsvc
  cd "${srcdir}/${_libname}-${pkgver}-src/src/native/unix"
  ./configure
  make
  docbook2man man/jsvc.1.xml
}

package_java-commons-daemon() {
  pkgdesc="Set of Apache libraries for making Java applications run on UNIX more easily"
  arch=('any')
  depends=('java-runtime')

  cd "${srcdir}/${_libname}-${pkgver}-src"

  install -Dm755 dist/${_libname}-${pkgver}.jar \
                 ${pkgdir}/usr/share/java/${_libname}-${pkgver}.jar
  ln -s ${_libname}-${pkgver}.jar \
        ${pkgdir}/usr/share/java/${_libname}.jar
}

package_java-jsvc() {
  pkgdesc="Apache application for making Java applications run on UNIX more easily"
  arch=('i686' 'x86_64')
  depends=("java-${_libname}")

  # jsvc
  cd "${srcdir}/${_libname}-${pkgver}-src/src/native/unix"

  install -Dm755 jsvc ${pkgdir}/usr/bin/jsvc
  install -Dm644 JSVC.1 ${pkgdir}/usr/share/man/man1/JSVC.1
}

