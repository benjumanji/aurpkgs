# Contributor: Hugo Doria <hugo@archlinux.org>
# Contributor: Guillaume ALAUX <guillaume at alaux dot net>
pkgname=tomcat5
pkgver=5.5.30
pkgrel=1
arch=('any')
pkgdesc="Servlet container for Java Servlet and JavaServer Pages"
url="http://tomcat.apache.org/"
license=('APACHE')
depends=('java-environment')
makedepends=('java-environment')
backup=('usr/share/tomcat5/conf/server.xml'
	'usr/share/tomcat5/conf/tomcat-users.xml'
	'etc/conf.d/tomcat5')
optdepends='tomcat5-admin'
install=tomcat5.install
source=(http://archive.apache.org/dist/tomcat/tomcat-5/v${pkgver}/bin/apache-tomcat-${pkgver}.tar.gz
	'tomcat5'
	'tomcat5.conf.d')
md5sums=('661dd3fbc25249e9ceb86652eb6a4f07'
         '6b113d1e4620ebb8225f37ba36cce4b1'
         '12620cce72c460a8e274e77dd5f42a46')

build() {
	unset LDFLAGS
	# build jsvc
	cd ${srcdir}/apache-tomcat-${pkgver}/bin
	tar xzf commons-daemon-native.tar.gz
	cd commons-daemon-1.0.2-native-src/unix
	sh configure
	make clean
	make || return 1
	cp jsvc ../..

	# get rid of some cruft
	cd ${srcdir}/apache-tomcat-${pkgver}
	rm -f LICENSE NOTICE RELEASE-NOTES RUNNING.txt
	rm -fr bin/*.exe bin/*.bat bin/*.tar.gz bin/*64/*.exe bin/jsvc-src logs

	# install everything
	mkdir -p ${pkgdir}/usr/share/${pkgname} \
		${pkgdir}/srv/http/${pkgname} \
		${pkgdir}/var/log/${pkgname}
	
	sed -i 's|<Host name="localhost" appBase="webapps"|<Host name="localhost" appBase="/srv/http/tomcat5/webapps"|g' conf/server.xml
	mv webapps ${pkgdir}/srv/http/${pkgname}
	ln -s /srv/http/${pkgname}/webapps ${pkgdir}/usr/share/${pkgname}/webapps
	
	cp -R * ${pkgdir}/usr/share/${pkgname}

	install -D -m755 ${srcdir}/tomcat5 ${pkgdir}/etc/rc.d/tomcat5
	install -D -m644 ${srcdir}/tomcat5.conf.d ${pkgdir}/etc/conf.d/tomcat5

	ln -s /var/log/${pkgname} ${pkgdir}/usr/share/${pkgname}/logs

	chgrp -R 66 ${pkgdir}/usr/share/${pkgname}/conf ${pkgdir}/usr/share/${pkgname}/work
	chmod 0660 ${pkgdir}/usr/share/${pkgname}/conf/{*.xml,*.policy,*.properties,/Catalina/localhost/*.xml}
	chmod 0775 ${pkgdir}/usr/share/${pkgname}/conf ${pkgdir}/usr/share/${pkgname}/work
}
