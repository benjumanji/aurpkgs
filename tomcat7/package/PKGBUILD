# Maintainer: Guillaume ALAUX <guillaume@archlinux.org>
pkgname=tomcat
pkgver=7.0.12
pkgrel=1
pkgdesc="Servlet-3.0/JSP-2.2 Container"
arch=('any')
url='http://tomcat.apache.org/'
license=('APACHE')
depends=('java-runtime>=6' 'eclipse-ecj' 'java-commons-daemon')
makedepends=('java-environment>=6' 'apache-ant>=1.6' 'eclipse-ecj' 'java-commons-daemon')
optdepends=('tomcat-native')
backup=(etc/tomcat/catalina.policy
        etc/tomcat/catalina.properties
        etc/tomcat/context.xml
        etc/tomcat/logging.properties
        etc/tomcat/server.xml
        etc/tomcat/tomcat-users.xml
        etc/tomcat/web.xml)
install=tomcat.install
source=(http://archive.apache.org/dist/tomcat/tomcat-7/v${pkgver}/src/apache-tomcat-${pkgver}-src.tar.gz
        http://tomcat.apache.org/dev/dist/m2-repository/org/apache/tomcat/dbcp/${pkgver}/dbcp-${pkgver}.jar
        tomcat.conf.d
        tomcat
        tomcat.install)

noextract=(dbcp-${pkgver}.jar)
md5sums=('19a1eaa9c9938b520d3c360d8cf4af22'
         'c5b7101dbe6ed66624db6eca70d7873a'
         '84e819ababbc8e1ac5c325d8af7febf8'
         '2760f0a3f3a9585c7a4f8bdd7100c45f'
         '89617779ce76658d7c4b8c603bcbd38b')
build() {
  cd "${srcdir}/apache-tomcat-${pkgver}-src"

  mv ${srcdir}/dbcp-${pkgver}.jar ${srcdir}/tomcat-dbcp.jar

  . /etc/profile.d/apache-ant.sh
  ant \
    -lib /usr/share/java \
    -Dtomcat-dbcp.jar=${srcdir}/tomcat-dbcp.jar \
    -Dcommons-daemon.jar=/usr/share/java/commons-daemon.jar \
    -Djdt.jar=/usr/share/java/ecj.jar \
    -Dbase.path=${srcdir}/DOWN_LIBS \
    download deploy
}

package() {
  cd "${srcdir}/apache-tomcat-${pkgver}-src/output/build"

  # Tomcat general files
  install -dm755 ${pkgdir}/usr/share/{,java/}${pkgname}
  cp -r bin ${pkgdir}/usr/share/${pkgname}
  rm ${pkgdir}/usr/share/${pkgname}/bin/{*.bat,commons-daemon*,tomcat-native.tar.gz}

  install lib/* ${pkgdir}/usr/share/java/${pkgname}
  rm ${pkgdir}/usr/share/java/${pkgname}/ecj.jar
  ln -s /usr/share/java/${pkgname} ${pkgdir}/usr/share/${pkgname}/lib

  # 66=tomcat / 19=log
  install -dm775 -o 66 -g 19 ${pkgdir}/var/log/${pkgname}
  ln -s /var/log/${pkgname} ${pkgdir}/usr/share/${pkgname}/logs

  install -dm770 ${pkgdir}/etc/${pkgname}
  install -m660 conf/* ${pkgdir}/etc/${pkgname}
  ln -s /etc/${pkgname} ${pkgdir}/usr/share/${pkgname}/conf

  install -dm775 ${pkgdir}/var/lib/${pkgname}
  cp -r webapps ${pkgdir}/var/lib/${pkgname}
  chown -R 66:66 ${pkgdir}/var/lib/${pkgname}
  ln -s /var/lib/${pkgname}/webapps ${pkgdir}/usr/share/${pkgname}/webapps

  install -dm1777 ${pkgdir}/var/tmp
  install -dm755 -o 66 -g 66 ${pkgdir}/var/tmp/${pkgname}/{temp,work}
  ln -s /var/tmp/${pkgname}/temp ${pkgdir}/usr/share/${pkgname}/temp
  ln -s /var/tmp/${pkgname}/work ${pkgdir}/usr/share/${pkgname}/work

  # Arch scripts
  install -Dm644 ${srcdir}/tomcat.conf.d ${pkgdir}/etc/conf.d/${pkgname}
  install -Dm755 ${srcdir}/tomcat ${pkgdir}/etc/rc.d/${pkgname}
}

