# Contributor: Geoffroy Carrier <geoffroy.carrier@aur.archlinux.org>
# Contributor: Jason Chu <jason@archlinux.org>
# Maintainer: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: Guillaume ALAUX <Guillaume at alaux dot net>

pkgname=('jre' 'jdk')
pkgbase=java-sun
pkgver=6u21
pkgrel=1
pkgdesc="Sun/Oracle's implementation of tha Java 6 specification"
arch=('i686' 'x86_64')
	[ "$CARCH" = "i686" ]   && _arch=i586
	[ "$CARCH" = "x86_64" ] && _arch=amd64
url="http://java.sun.com"
license=('custom')
source=(http://download.java.net/dlj/binaries/jdk-${pkgver}-dlj-linux-${_arch}.bin
	jdk.profile
	java-control-panel.desktop
	java-monitoring-and-management-console.desktop
	java-policy-settings.desktop
	java-visualvm.desktop
	java-web-start.desktop
	jre.profile)

[ "$CARCH" = "i686" ]   && md5sums=('b3fbbe55bafedb3f8f93e91d4e809390'
				'7aaac9ec3e440b92361c6561ac00424a'
				'e4d814c0f310d77ed6990c731bccd0fb'
				'4bc2b0be93f76ed07b471e3c89eb5a8a'
				'8e3cec7ab641ec449cabd068be583f5c'
				'9ba148ebabce8ed3351b813a9a66b6e5'
				'568451cf45fc8c7adb83a222a61ca2c6'
				'50b4f5ac4129097461d246645d73a622')
[ "$CARCH" = "x86_64" ] && md5sums=('7961051f95c014a9b94ffe3e95ea66e6'
				'7aaac9ec3e440b92361c6561ac00424a'
				'e4d814c0f310d77ed6990c731bccd0fb'
				'4bc2b0be93f76ed07b471e3c89eb5a8a'
				'8e3cec7ab641ec449cabd068be583f5c'
				'9ba148ebabce8ed3351b813a9a66b6e5'
				'568451cf45fc8c7adb83a222a61ca2c6'
				'50b4f5ac4129097461d246645d73a622')

build() {
	cd ${srcdir}
	mkdir unbundle-jdk
	cd unbundle-jdk
	sh ../jdk-${pkgver}-dlj-linux-$_arch.bin --accept-license
	cd ..

	# Construct JDK/JRE
	# https://jdk-distros.dev.java.net/source/browse/*checkout*/jdk-distros/trunk/utils/construct.sh
	# unpacking jars...
	cd unbundle-jdk/jdk1.6.0_21
	for i in $(find . -name '*.pack'); do
		jdir=$(dirname $i)
		jbase=$(basename $i .pack)
		if ! unpack200 $jdir/$jbase.pack $jdir/$jbase.jar; then
			echo "error unpacking $jdir/$jbase.jar"
			exit 1
		fi
		if [ ! -r $jdir/$jbase.jar ]; then
			echo "missing $jdir/$jbase.jar"
			exit 1
		else
			# remove pack file
			rm $jdir/$jbase.pack
		fi
	done

	# if this is a client installation...
	compiler="$(bin/java -client -version 2>&1 | tail -n +3 | cut -d' ' -f1-4)"
	if [ "X$compiler" = "XJava HotSpot(TM) Client VM" ]; then
		# create the CDS archive
		# creating the class data sharing archive...
		if ! bin/java -client -Xshare:dump > /dev/null 2>&1; then
			echo "returned error code $?"
		fi
	fi

	# Setting up the JRE man pages...
	mkdir -p jre/man/{,ja_JP.eucJP/}man1
	# javaws.1 kinit.1 klist.1 ktab.1
	for f in java.1 keytool.1 orbd.1 pack200.1 policytool.1 \
		rmid.1 rmiregistry.1 servertool.1 tnameserv.1 unpack200.1; do

		mv man/man1/$f jre/man/man1/$f
		ln -s jre/man/man1/$f man/man1/$f

		mv man/ja_JP.eucJP/man1/$f jre/man/ja_JP.eucJP/man1/$f
		ln -s jre/man/ja_JP.eucJP/man1/$f man/ja_JP.eucJP/man1/$f
	  done

	ln -s ja_JP.eucJP jre/man/ja

	# Eliminating duplication between the JDK and JDK/jre...
	# kinit klist ktab
	for f in ControlPanel java javaws keytool orbd pack200 policytool \
		rmid rmiregistry servertool tnameserv unpack200; do

		rm bin/$f
		ln -s ../jre/bin/$f bin/$f
	done
}

package_jre() {
	pkgdesc="Sun/Oracle's Java Runtime Environment"
	depends=('glibc' 'libxtst')
	provides=('j2re' 'java-runtime=6')
	conflicts=('j2re' 'java-runtime')
	replaces=('j2re')
	install='jre.install'

	cd ${srcdir}/unbundle-jdk/jdk1.6.0_21

	mkdir -p ${pkgdir}/opt/java
	cp -R jre ${pkgdir}/opt/java

	install -Dm755 ${srcdir}/jre.profile ${pkgdir}/etc/profile.d/jre.sh

	mkdir -p ${pkgdir}/usr/lib/mozilla/plugins

	if [ "$CARCH" = "i686" ]; then
		ln -s /opt/java/jre/lib/i386/libnpjp2.so ${pkgdir}/usr/lib/mozilla/plugins
	else	ln -s /opt/java/jre/lib/amd64/libnpjp2.so ${pkgdir}/usr/lib/mozilla/plugins
	fi

	mkdir -p ${pkgdir}/usr/share/licenses/jre
	cp ${pkgdir}/opt/java/jre/COPYRIGHT ${pkgdir}/usr/share/licenses/jre
	cp ${pkgdir}/opt/java/jre/LICENSE ${pkgdir}/usr/share/licenses/jre
	cp ${pkgdir}/opt/java/jre/THIRDPARTYLICENSEREADME.txt ${pkgdir}/usr/share/licenses/jre

	# Fix system prefs folder (FS#18872)
	install -dm755 ${pkgdir}/etc/.java/.systemPrefs
}

package_jdk() {
	pkgdesc="Sun/Oracle's Java Development Kit"
	depends=('glibc' 'jre')
	provides=('java-environment=6' 'j2sdk')
	conflicts=('java-environment' 'j2sdk')
	replaces=('j2sdk')
	install='jdk.install'

	cd ${srcdir}

	#copy icon
	install -Dm644 ${srcdir}/unbundle-jdk/jdk1.6.0_21/jre/lib/desktop/icons/hicolor/48x48/apps/sun-java.png \
		 ${pkgdir}/usr/share/pixmaps/java.png

	rm -rf unbundle-jdk/jdk1.6.0_21/jre
	mkdir -p ${pkgdir}/opt
	mv unbundle-jdk/jdk1.6.0_21 ${pkgdir}/opt/java


	install -D -m755 ${srcdir}/jdk.profile ${pkgdir}/etc/profile.d/jdk.sh

	mkdir -p ${pkgdir}/usr/share/licenses/jdk
	cp ${pkgdir}/opt/java/COPYRIGHT ${pkgdir}/usr/share/licenses/jdk
	cp ${pkgdir}/opt/java/LICENSE ${pkgdir}/usr/share/licenses/jdk
	cp ${pkgdir}/opt/java/THIRDPARTYLICENSEREADME.txt ${pkgdir}/usr/share/licenses/jdk

	#desktop entries
	install -Dm644 ${srcdir}/java-control-panel.desktop \
  		 ${pkgdir}/usr/share/applications/java-control-panel.desktop
	install -Dm644 ${srcdir}/java-monitoring-and-management-console.desktop \
  		 ${pkgdir}/usr/share/applications/java-monitoring-and-management-console.desktop
	install -Dm644 ${srcdir}/java-policy-settings.desktop \
  		 ${pkgdir}/usr/share/applications/java-policy-settings.desktop
	install -Dm644 ${srcdir}/java-visualvm.desktop \
  		 ${pkgdir}/usr/share/applications/java-visualvm.desktop
	install -Dm644 ${srcdir}/java-web-start.desktop \
  		 ${pkgdir}/usr/share/applications/java-web-start.desktop
}
