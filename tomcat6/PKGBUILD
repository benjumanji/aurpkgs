# Contributor: Hugo Doria <hugo@archlinux.org>
# Contributor: Guillaume ALAUX <guillaume at alaux dot net>
pkgname=tomcat6
pkgver=6.0.28
pkgrel=1
arch=('any')
pkgdesc="Servlet container for Java Servlet and JavaServer Pages"
url="http://tomcat.apache.org/"
license=('APACHE')
depends=('java-environment')
makedepends=('java-environment')
backup=('usr/share/tomcat6/conf/server.xml'
	'usr/share/tomcat6/conf/tomcat-users.xml'
	'etc/conf.d/tomcat6')
#optdepends='tomcat5-admin'
install=tomcat6.install
source=(http://archive.apache.org/dist/tomcat/tomcat-6/v${pkgver}/bin/apache-tomcat-${pkgver}.tar.gz
	'tomcat6'
	'tomcat6.conf.d')
md5sums=('e79253eff8e851a2c2bf9d42e58b0a78'
         '002dfa4548f57f737909e0fca8902239'
         '6cdcd0c3b6418d015e6bc4e93c1ecf90')

build() {
	unset LDFLAGS
	# build jsvc
	cd ${srcdir}/apache-tomcat-${pkgver}/bin
	tar xzf commons-daemon-native.tar.gz
	cd commons-daemon-1.0.2-native-src/unix
	sh configure
	make clean
	make || return 1
	cp jsvc ../..

	# get rid of some cruft
	cd ${srcdir}/apache-tomcat-${pkgver}
	rm -f LICENSE NOTICE RELEASE-NOTES RUNNING.txt
	rm -fr bin/*.bat bin/*.tar.gz logs

	# install everything
	mkdir -p ${pkgdir}/usr/share/${pkgname} \
		${pkgdir}/srv/http/${pkgname} \
		${pkgdir}/var/log/${pkgname}
	
	sed -i 's|<Host name="localhost" appBase="webapps"|<Host name="localhost" appBase="/srv/http/tomcat6/webapps"|g' conf/server.xml
	mv webapps ${pkgdir}/srv/http/${pkgname}
	ln -s /srv/http/${pkgname}/webapps ${pkgdir}/usr/share/${pkgname}/webapps
	
	cp -R * ${pkgdir}/usr/share/${pkgname}

	install -D -m755 ${srcdir}/tomcat6 ${pkgdir}/etc/rc.d/tomcat6
	install -D -m644 ${srcdir}/tomcat6.conf.d ${pkgdir}/etc/conf.d/tomcat6

	ln -s /var/log/${pkgname} ${pkgdir}/usr/share/${pkgname}/logs

	chgrp -R 66 ${pkgdir}/usr/share/${pkgname}/conf ${pkgdir}/usr/share/${pkgname}/work
	chmod 0660 ${pkgdir}/usr/share/${pkgname}/conf/{*.xml,*.policy,*.properties}
	chmod 0775 ${pkgdir}/usr/share/${pkgname}/conf ${pkgdir}/usr/share/${pkgname}/work
}
