# $Id: PKGBUILD 81150 2010-05-26 20:31:33Z ibiru $
# Contributor: Hugo Doria <hugo@archlinux.org>

pkgname=tomcat
pkgver=5.5.29
pkgrel=1
arch=('i686' 'x86_64')
pkgdesc="Servlet container for Java Servlet and JavaServer Pages"
url="http://tomcat.apache.org/"
license=('APACHE')
depends=('java-environment')
makedepends=('java-environment')
backup=('usr/share/tomcat/conf/server.xml' 'usr/share/tomcat/conf/tomcat-users.xml' 'etc/conf.d/tomcat')
install=tomcat.install
source=(http://archive.apache.org/dist/tomcat/tomcat-5/v${pkgver}/bin/apache-tomcat-${pkgver}.tar.gz
	'tomcat'
	'tomcat.conf.d')
md5sums=('228496a9cfe3aaa3b7e79386ccd9380a'
         'bdb5d655c5e20301a07106ab59d2e0b3'
         'dc7ba215d4629003cbd229b526d08501')

build() {
	unset LDFLAGS
	# build jsvc
	cd ${srcdir}/apache-${pkgname}-${pkgver}/bin
	tar xzf jsvc.tar.gz
	cd jsvc-src
	sh configure
	make || return 1
	cp jsvc ..

	# get rid of some cruft
	cd ${srcdir}/apache-${pkgname}-${pkgver}
	rm -f LICENSE NOTICE RELEASE-NOTES RUNNING.txt
	rm -fr bin/*.exe bin/*.bat bin/*.tar.gz bin/*64/*.exe bin/jsvc-src

	# install everything
	mkdir -p ${pkgdir}/usr/share/${pkgname}
	# ${pkgdir}/usr/share/java/${pkgname}
	cp -R * ${pkgdir}/usr/share/${pkgname}

	install -D -m755 ${srcdir}/tomcat ${pkgdir}/etc/rc.d/tomcat
	install -D -m644 ${srcdir}/tomcat.conf.d ${pkgdir}/etc/conf.d/tomcat
	chgrp -R 66 ${pkgdir}/usr/share/tomcat/conf ${pkgdir}/usr/share/tomcat/work
	chmod 0660 ${pkgdir}/usr/share/tomcat/conf/{*.xml,*.policy,*.properties,/Catalina/localhost/*.xml}
	chmod 775 ${pkgdir}/usr/share/tomcat/conf ${pkgdir}/usr/share/tomcat/work

	mkdir -p ${pkgdir}/var/log/tomcat
}
