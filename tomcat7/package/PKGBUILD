# Maintainer: Guillaume ALAUX <guillaume@archlinux.org>
pkgname=tomcat
pkgver=7.0.12
pkgrel=1
pkgdesc="Servlet-3.0/JSP-2.2 Container"
arch=('any')
url='http://tomcat.apache.org/'
license=('APACHE')
depends=('java-runtime>=6' 'java-commons-daemon' 'eclipse-ecj')
makedepends=('java-environment>=6' 'apache-ant>=1.6' 'java-commons-daemon' 'eclipse-ecj')
optdepends=('tomcat-native: to allow optimal performance in production environments')
backup=(etc/tomcat/catalina.policy
        etc/tomcat/catalina.properties
        etc/tomcat/context.xml
        etc/tomcat/logging.properties
        etc/tomcat/server.xml
        etc/tomcat/tomcat-users.xml
        etc/tomcat/web.xml)
install=tomcat.install
source=(http://archive.apache.org/dist/tomcat/tomcat-7/v${pkgver}/src/apache-tomcat-${pkgver}-src.tar.gz
        tomcat.conf.d
        tomcat
        tomcat.install)

noextract=(dbcp-${pkgver}.jar)
md5sums=('edfd253c21b51ccadce8ec05be5e61fb'
         '11a30b7d3542ef0b62857a01c0d2acbd'
         'b27364a5565a7c8c7e9c13a3f4b987bb'
         '1be39071bd27781c352b2d73b765e29d')
build() {
  cd "${srcdir}/apache-tomcat-${pkgver}-src"

  . /etc/profile.d/apache-ant.sh
  ant \
    -Dbase.path=${srcdir}/DOWN_LIBS \
    -Dcommons-daemon.jar=/usr/share/java/commons-daemon.jar \
    -Djdt.jar=/usr/share/java/eclipse-ecj.jar
}

package() {
  cd "${srcdir}/apache-tomcat-${pkgver}-src/output/build"

  # Tomcat general files
  install -dm755 ${pkgdir}/usr/share/{,java/}${pkgname}
  #rm bin/{*.bat,commons-daemon*,tomcat-native.tar.gz}
  rm bin/{*.bat,commons-daemon*}
  cp -r bin ${pkgdir}/usr/share/${pkgname}

  rm lib/eclipse-ecj.jar
  install lib/* ${pkgdir}/usr/share/java/${pkgname}
  ln -s /usr/share/java/${pkgname} ${pkgdir}/usr/share/${pkgname}/lib

  # 66=tomcat / 19=log
  install -dm775 -o 66 -g 19 ${pkgdir}/var/log/${pkgname}
  ln -s /var/log/${pkgname} ${pkgdir}/usr/share/${pkgname}/logs

  install -dm770 ${pkgdir}/etc/${pkgname}
  install -m660 conf/* ${pkgdir}/etc/${pkgname}
  ln -s /etc/${pkgname} ${pkgdir}/usr/share/${pkgname}/conf

  install -dm775 ${pkgdir}/var/lib/${pkgname}
  cp -r webapps ${pkgdir}/var/lib/${pkgname}
  chown -R 66:66 ${pkgdir}/var/lib/${pkgname}
  ln -s /var/lib/${pkgname}/webapps ${pkgdir}/usr/share/${pkgname}/webapps

  install -dm1777 ${pkgdir}/var/tmp
  install -dm755 -o 66 -g 66 ${pkgdir}/var/tmp/${pkgname}/{temp,work}
  ln -s /var/tmp/${pkgname}/temp ${pkgdir}/usr/share/${pkgname}/temp
  ln -s /var/tmp/${pkgname}/work ${pkgdir}/usr/share/${pkgname}/work

  # Arch scripts
  install -Dm644 ${srcdir}/tomcat.conf.d ${pkgdir}/etc/conf.d/${pkgname}
  install -Dm755 ${srcdir}/tomcat ${pkgdir}/etc/rc.d/${pkgname}
}

