# $Id: PKGBUILD 109160 2011-02-06 21:19:55Z guillaume $
# Maintainer: Guillaume ALAUX <guillaume at archlinux dot org>
# Contributor: Hugo Doria <hugo@archlinux.org>
pkgname=tomcat5.5
pkgver=5.5.32
pkgrel=1
arch=('i686' 'x86_64')
pkgdesc="Apache Tomcat 5 Servlet-2.4/JSP-2.0 Container"
url="http://tomcat.apache.org/"
license=('APACHE')
depends=('java-environment>=1.4')
makedepends=('openjdk6')
backup=('opt/tomcat/conf/server.xml' 'opt/tomcat/conf/tomcat-users.xml' 'etc/conf.d/tomcat')
install=tomcat.install
source=(http://archive.apache.org/dist/tomcat/tomcat-5/v${pkgver}/bin/apache-tomcat-${pkgver}.tar.gz
	'tomcat'
	'tomcat.conf.d')
md5sums=('ce7a455da223977133577ffd9cafe233'
         '06be2138fcf280ab33799f3db90dfe7a'
         '368322d8a51ce20cc5dbdb505706a16a')

build() {
  unset LDFLAGS
  # build jsvc
  cd ${srcdir}/apache-tomcat-${pkgver}/bin
  tar xzf commons-daemon-native.tar.gz
  cd commons-daemon-1.0.5-native-src/unix
  sh configure --with-java=/usr/lib/jvm/java-6-openjdk
  make clean
  make
  cp jsvc ../..

  # get rid of some cruft
  cd ${srcdir}/apache-tomcat-${pkgver}
  rm -f LICENSE NOTICE RELEASE-NOTES RUNNING.txt
  rm -fr bin/*.exe bin/*.bat bin/*.tar.gz bin/jsvc-src

  # install everything
  mkdir -p ${pkgdir}/opt/tomcat
  cp -R * ${pkgdir}/opt/tomcat
  install -D -m755 ${srcdir}/tomcat ${pkgdir}/etc/rc.d/${pkgname}
  install -D -m644 ${srcdir}/tomcat.conf.d ${pkgdir}/etc/conf.d/${pkgname}
  chgrp -R 66 ${pkgdir}/opt/tomcat/{conf,work,webapps}
  chmod 0660 ${pkgdir}/opt/tomcat/conf/{*.xml,*.policy,*.properties,/Catalina/localhost/*.xml}
  chmod 775 ${pkgdir}/opt/tomcat/{conf,work,webapps}
}
