# Contributor: Hugo Doria <hugo@archlinux.org>
# Contributor: Guillaume ALAUX <guillaume at alaux dot net>

pkgname=tomcat
pkgver=5.5.30
pkgrel=1
arch=('any')
pkgdesc="Servlet container for Java Servlet and JavaServer Pages"
url="http://tomcat.apache.org/"
license=('APACHE')
depends=('java-environment')
makedepends=('java-environment')
backup=('usr/share/tomcat/conf/server.xml'
	'usr/share/tomcat/conf/tomcat-users.xml'
	'etc/conf.d/tomcat')
optdepends='tomcat-admin'
install=tomcat.install
source=(http://archive.apache.org/dist/tomcat/tomcat-5/v${pkgver}/bin/apache-tomcat-${pkgver}.tar.gz
	'tomcat'
	'tomcat.conf.d')
md5sums=('661dd3fbc25249e9ceb86652eb6a4f07'
         'bdb5d655c5e20301a07106ab59d2e0b3'
         'dc7ba215d4629003cbd229b526d08501')

build() {
	unset LDFLAGS
	# build jsvc
	cd ${srcdir}/apache-${pkgname}-${pkgver}/bin
	tar xzf commons-daemon-native.tar.gz
	cd commons-daemon-1.0.2-native-src/unix
	sh configure
	make clean
	make || return 1
	cp jsvc ../..

	# get rid of some cruft
	cd ${srcdir}/apache-${pkgname}-${pkgver}
	rm -f LICENSE NOTICE RELEASE-NOTES RUNNING.txt
	rm -fr bin/*.exe bin/*.bat bin/*.tar.gz bin/*64/*.exe bin/jsvc-src logs

	# install everything
	mkdir -p ${pkgdir}/usr/share/${pkgname} \
		${pkgdir}/srv/${pkgname} \
		${pkgdir}/var/log/${pkgname}
	
	sed -i 's|<Host name="localhost" appBase="webapps"|<Host name="localhost" appBase="/srv/tomcat/webapps"|g' conf/server.xml
	mv webapps ${pkgdir}/srv/${pkgname}
	ln -s /srv/${pkgname}/webapps ${pkgdir}/usr/share/${pkgname}/webapps
	
	cp -R * ${pkgdir}/usr/share/${pkgname}

	install -D -m755 ${srcdir}/tomcat ${pkgdir}/etc/rc.d/tomcat
	install -D -m644 ${srcdir}/tomcat.conf.d ${pkgdir}/etc/conf.d/tomcat

	ln -s /var/log/${pkgname} ${pkgdir}/usr/share/${pkgname}/logs

	chgrp -R 66 ${pkgdir}/usr/share/tomcat/conf ${pkgdir}/usr/share/tomcat/work
	chmod 0660 ${pkgdir}/usr/share/tomcat/conf/{*.xml,*.policy,*.properties,/Catalina/localhost/*.xml}
	chmod 0775 ${pkgdir}/usr/share/tomcat/conf ${pkgdir}/usr/share/tomcat/work
}
