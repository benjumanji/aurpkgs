# Maintainer: Guillaume ALAUX <guillaume@archlinux.org>
pkgname=('jre8-openjdk-headless-noicedtea' 'jre8-openjdk-noicedtea' 'jdk8-openjdk-noicedtea')
pkgbase=java8-openjdk
_java_ver=8
_codate=2014-03-28
_JDK_UPDATE_VERSION=132
_hgdir=jdk8_hg_$_codate
pkgver=${_java_ver}.u${_JDK_UPDATE_VERSION}
pkgrel=1
arch=('i686' 'x86_64')
url='http://openjdk.java.net/'
license=('custom')
makedepends=('jdk7-openjdk' 'ccache' 'mercurial' 'cpio' 'unzip' 'zip'
             'libxrender' 'libxtst' 'fontconfig' 'libcups' 'alsa-lib')
              #'giflib'
source=($_hgdir.tgz
        001_adjust-mflags-for-gmake-4.patch
        profile_jdk8-openjdk.csh
        profile_jdk8-openjdk.sh
        profile_jre8-openjdk.csh
        profile_jre8-openjdk.sh)

sha256sums=('a5017c01889551d04acfb946e15b7fdb54c92f2c0a607276ff7863362127dd8d'
            '682104f7723c5c543c47b53f51cc5577e273a12343bd04bebd1bcd70dd72ded9'
            '008296c931817bb95727af87cda4c292139f159436a1122a039b7850c6bec474'
            'bd155a7ae70b6bebdbba95de73a9cfb3c8e9d8b4ce2a92c38201c486f5fd8427'
            'd6902559ad121e10ac005b66e6e3b08f5ec9d3755800243db92580c0dd9d096a'
            '3a841d8b5732b8995dbeb8851ba7751249f8f0985946bfc2a8f74c618e9712c9')

_jvmdir=/usr/lib/jvm/java-8-openjdk
_headlessfiles=(bin/policytool
                lib/amd64/libjsound.so
                lib/amd64/libjsoundalsa.so
                lib/amd64/libsplashscreen.so)

mksource() {
  cd $srcdir

  # Initial checkout
  #hg clone http://hg.openjdk.java.net/jdk8/jdk8 $_hgdir

  cd $_hgdir
  # To update the whole forest:
  sh get_source.sh

  cd $srcdir
  tar czf $_hgdir.tgz $_hgdir
}

prepare() {
  cd "$srcdir/$_hgdir"
  hg checkout jdk8-b$_JDK_UPDATE_VERSION

  # https://bugs.openjdk.java.net/browse/JDK-8028407
  cd hotspot
  patch -p1 < $srcdir/001_adjust-mflags-for-gmake-4.patch
}

build() {
  cd "$srcdir/$_hgdir"

  unset JAVA_HOME
  # http://icedtea.classpath.org/bugzilla/show_bug.cgi?id=1346
  export MAKEFLAGS=${MAKEFLAGS/-j*}

  install -dm755 $srcdir/$_hgdir/image
  sh configure \
    --prefix=$srcdir/$_hgdir/image \
    --with-milestone='ga' \
    --with-build-number="b$_JDK_UPDATE_VERSION" \
    --with-zlib=system
    # TODO --enable-unlimited-crypto \
    # TODO seems like openjdk does not want last version of giflib
    #--with-giflib=system \
    # TODO Cannot use 'pkgdir' in 'build' as split package does not know what this can possibly mean
    #--prefix=$srcdir/image \
    # TODO do we need these?
    #--with-update-version=$_JDK_UPDATE_VERSION \
    #--with-user-release-suffix='archlinux' \
    # TODO does not work? Plus create links refering to $pkgdir
    #--bindir=$pkgdir/usr/bin \
    # TODO add these if not already taken by default
#  --with-freetype-lib
#  --with-alsa-lib
 
  make
  #LOG=trace HOTSPOT_BUILD_JOBS=1

  unset JAVA_HOME
  # http://icedtea.classpath.org/bugzilla/show_bug.cgi?id=1346
  export MAKEFLAGS=${MAKEFLAGS/-j*}
  # FIXME sadly 'DESTDIR' is not used here!
  make DESTDIR="$pkgdir/" install

  cd "$srcdir/$_hgdir/image/jvm/openjdk-1.8.0-ga"

  # A lot of stuff were directly taken from
  # http://pkgs.fedoraproject.org/cgit/java-1.8.0-openjdk.git/tree/java-1.8.0-openjdk.spec

  # http://icedtea.classpath.org/bugzilla/show_bug.cgi?id=1437
  find . -iname '*.jar' -exec chmod ugo+r {} \;
  chmod ugo+r lib/ct.sym

  # remove redundant *diz and *debuginfo files
  find . -iname '*.diz' -exec rm {} \;
  find . -iname '*.debuginfo' -exec rm {} \;
}

#check() {
#  cd "$srcdir/$pkgname-$pkgver"
#  make -k check
#}

package_jre8-openjdk-headless-noicedtea() {
  pkgdesc="Free Java environment - headless runtime environment - 'NO ICEDTEA' DEVEL VERSION"
  depends=('ca-certificates-java' 'nss')
  optdepends=('java-rhino: for some JavaScript support')
  provides=('java-runtime-headless=8')
  conflicts=('java-runtime-headless')
  # FIXME first file looks like it belongs to 64 arch only…
  # Config files from 'lib' dir that should be backup
  _backup_etc=(etc/java-8-openjdk/amd64/jvm.cfg
               etc/java-8-openjdk/calendars.properties
               etc/java-8-openjdk/content-types.properties
               etc/java-8-openjdk/flavormap.properties
               etc/java-8-openjdk/images/cursors/cursors.properties
               etc/java-8-openjdk/logging.properties
               etc/java-8-openjdk/management/jmxremote.access
               etc/java-8-openjdk/management/jmxremote.password
               etc/java-8-openjdk/management/management.properties
               etc/java-8-openjdk/management/snmp.acl
               etc/java-8-openjdk/net.properties
               etc/java-8-openjdk/psfont.properties.ja
               etc/java-8-openjdk/psfontj2d.properties
               etc/java-8-openjdk/security/java.policy
               etc/java-8-openjdk/security/java.security
               etc/java-8-openjdk/sound.properties)
  backup=(etc/profile.d/jre.sh
          etc/profile.d/jre.csh
          ${_backup_etc[@]})
  install=install_jre8-openjdk.sh

  cd "$srcdir/$_hgdir/image/jvm/openjdk-1.8.0-ga/jre"

  install -d -m755 ${pkgdir}${_jvmdir}/jre
  cp -a bin lib ${pkgdir}${_jvmdir}/jre

  # Remove 'non-headless' lib files
  for f in ${_headlessfiles[@]}; do
    rm ${pkgdir}${_jvmdir}/jre/$f
  done

  # Link binaries into /usr/bin
  pushd ${pkgdir}${_jvmdir}/jre/bin
    install -d -m755 ${pkgdir}/usr/bin/
    install -d -m755 $pkgdir/usr/share/man/{,ja/}man1/
    for file in *; do
      ln -s ${_jvmdir}/jre/bin/${file} ${pkgdir}/usr/bin
      install -m644 "$srcdir/$_hgdir/image/jvm/openjdk-1.8.0-ga/man/man1/$file.1" \
        $pkgdir/usr/share/man/man1/$file.1
      install -m644 "$srcdir/$_hgdir/image/jvm/openjdk-1.8.0-ga/man/ja/man1/$file.1" \
        $pkgdir/usr/share/man/ja/man1/$file.1
    done
  popd

  # Link JKS keystore from ca-certificates-java
  rm -f ${pkgdir}${_jvmdir}/jre/lib/security/cacerts
  ln -sf /etc/ssl/certs/java/cacerts "${pkgdir}${_jvmdir}/jre/lib/security/cacerts"

  # TODO move these on a meta package to enable easy switching between different vendors and versions
  # Set some variables
  install -Dm755 ${srcdir}/profile_jre8-openjdk.sh  ${pkgdir}/etc/profile.d/jre.sh
  install -Dm755 ${srcdir}/profile_jre8-openjdk.csh ${pkgdir}/etc/profile.d/jre.csh

  # Install license
  install -m755 -d ${pkgdir}/usr/share/licenses/${pkgbase}/
  install -m644 ASSEMBLY_EXCEPTION LICENSE THIRD_PARTY_README \
                 ${pkgdir}/usr/share/licenses/${pkgbase}
  ln -sf /usr/share/licenses/${pkgbase} "${pkgdir}/usr/share/licenses/${pkgname}"

  mv ${pkgdir}${_jvmdir}/jre/lib/management/jmxremote.password.template \
     ${pkgdir}${_jvmdir}/jre/lib/management/jmxremote.password
  mv ${pkgdir}${_jvmdir}/jre/lib/management/snmp.acl.template \
     ${pkgdir}${_jvmdir}/jre/lib/management/snmp.acl

  # Move config files that were set in _backup_etc from ./lib to /etc
  for file in ${_backup_etc[@]}; do
    _filepkgpath=$_jvmdir/jre/lib/${file#etc/java-8-openjdk/}
    install -Dm644 $pkgdir$_filepkgpath $pkgdir/$file
    ln -sf /$file $pkgdir$_filepkgpath
  done
}

package_jre8-openjdk-noicedtea() {
  pkgdesc="Free Java environment - full runtime environment - 'NO ICEDTEA' DEVEL VERSION"
  depends=('java-runtime-headless')
  provides=('java-runtime=8')
  conflicts=('java-runtime')

  cd "$srcdir/$_hgdir/image/jvm/openjdk-1.8.0-ga/jre"

  # TODO? /usr/lib/jvm/java-8-openjdk/jre/lib/sound.properties
  for f in ${_headlessfiles[@]}; do
    install -Dm644 $f ${pkgdir}${_jvmdir}/jre/$f
  done

  # Link binaries into /usr/bin
  pushd ${pkgdir}${_jvmdir}/jre/bin
    install -m755 -d ${pkgdir}/usr/bin/
    ln -s ${_jvmdir}/jre/bin/policytool ${pkgdir}/usr/bin
  popd

  # Icons
  for s in 16 24 32 48; do
    install -Dm644 $srcdir/$_hgdir/jdk/src/solaris/classes/sun/awt/X11/java-icon${s}.png \
                   ${pkgdir}/usr/share/icons/hicolor/${s}x${s}/apps/java.png
  done

  # Desktop files
  # TODO add them when switching to IcedTea
  #install -Dm644 ${srcdir}/icedtea-${_icedtea_ver}/policytool.desktop \
  #               ${pkgdir}/usr/share/applications/policytool.desktop

  # man pages
  install -Dm644 "$srcdir/$_hgdir/image/jvm/openjdk-1.8.0-ga/man/man1/policytool.1" \
    $pkgdir/usr/share/man/man1/policytool.1
  install -Dm644 "$srcdir/$_hgdir/image/jvm/openjdk-1.8.0-ga/man/ja/man1/policytool.1" \
    $pkgdir/usr/share/man/ja/man1/policytool.1

  # Install license
  install -m755 -d ${pkgdir}/usr/share/licenses/${pkgbase}/
  ln -sf /usr/share/licenses/${pkgbase} "${pkgdir}/usr/share/licenses/${pkgname}"
}

package_jdk8-openjdk-noicedtea() {
  pkgdesc="Free Java environment - development kit - 'NO ICEDTEA' DEVEL VERSION"
  # TODO remove the '-noicedtea'
  depends=('jre8-openjdk-noicedtea' 'xdg-utils' 'hicolor-icon-theme')
  optdepends=('alsa-lib: for basic sound support'
             'gtk2: for the Gtk+ look and feel – desktop usage')
             #'icedtea-web-java8: web browser plugin + Java Web Start'
             #'giflib: for gif format support'
             #'libpulse: for advanced sound support'
  provides=('java-environment=8')
  conflicts=('java-environment')
  backup=(etc/profile.d/jdk.sh
          etc/profile.d/jdk.csh)

  cd "$srcdir/$_hgdir/image/jvm/openjdk-1.8.0-ga"

  # Main files
  install -m755 -d ${pkgdir}${_jvmdir}

  cp -a include lib ${pkgdir}${_jvmdir}

  # 'bin' files
  pushd bin
  install -m755 -d ${pkgdir}${_jvmdir}/bin/ \
                   ${pkgdir}/usr/bin/ \
                   ${pkgdir}/usr/share/man/{,ja/}man1/

  # 'java-rmi.cgi' will be handled separately as it should not be in the PATH and has no man page
  for b in $(ls | grep -v java-rmi.cgi); do
    if [ -e ../jre/bin/${b} ]; then
      # Provide a link of the jre binary in the jdk/bin/ directory
      ln -s ../jre/bin/${b} ${pkgdir}${_jvmdir}/bin/${b}
    else
      # Copy binary to jdk/bin/
      install -m755 ${b} ${pkgdir}${_jvmdir}/bin/${b}
      # Copy man page
      install -m644 ../man/man1/${b}.1 ${pkgdir}/usr/share/man/man1/${b}.1
      install -m644 ../man/ja/man1/${b}.1 ${pkgdir}/usr/share/man/ja/man1/${b}.1
      # Link from /bin/
      ln -s ${_jvmdir}/bin/${b} ${pkgdir}/usr/bin/${b}
    fi
  done
  popd

  # Handling 'java-rmi.cgi' separately
  install -m755 -D bin/java-rmi.cgi ${pkgdir}${_jvmdir}/bin/java-rmi.cgi

  # TODO Install desktop files.
  #install -m755 -d ${pkgdir}/usr/share/applications
  #install -m644 ${srcdir}/icedtea-${_icedtea_ver}/jconsole.desktop ${pkgdir}/usr/share/applications

  # Set some variables
  install -Dm755 ${srcdir}/profile_jdk8-openjdk.sh  ${pkgdir}/etc/profile.d/jdk.sh
  install -Dm755 ${srcdir}/profile_jdk8-openjdk.csh ${pkgdir}/etc/profile.d/jdk.csh

  # link license
  install -m755 -d ${pkgdir}/usr/share/licenses
  ln -sf /usr/share/licenses/${pkgbase} "${pkgdir}/usr/share/licenses/${pkgname}"
}
