# Maintainer: Guillaume ALAUX <Guillaume at ALAUX dot net>
# Contributor: Shizeeg Unadequatov <shizeeque@gmail.com>

pkgname=kakoune-git
pkgver=r2334.333134b
pkgrel=1
pkgdesc='Code editor heavily inspired by Vim'
arch=('i686' 'x86_64')
url='https://github.com/mawww/kakoune'
license=('custom:unlicense')
depends=('ncurses' 'boost' 'xorg-xmessage')
makedepends=('git')

source=("$pkgname::git+https://github.com/mawww/kakoune.git"
        '01_ncurses.patch')
sha256sums=('SKIP'
            'e9fc12fae5d57df4976510fdf1ebb52d2020498f5f427755888d76cb3bf071ce')

pkgver() {
  cd "${srcdir}/${pkgname}"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd "${srcdir}/${pkgname}"
  patch -p0 < "${srcdir}"/01_ncurses.patch
}

build() {
  cd "${srcdir}/${pkgname}/src"
  make
}

package() {
  cd "${srcdir}/${pkgname}/src"
  make install DESTDIR="${pkgdir}" PREFIX=/usr
  install -D ../UNLICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
