# $Id: PKGBUILD 44515 2009-07-02 11:42:20Z paul $
# Contributor: Andrew Wright <andreww@photism.org>
# Maintainer: Paul Mattal <paul@archlinux.org>

pkgname=apache-ant
pkgver=1.7.1
pkgrel=3
pkgdesc="A java-based build tool."
arch=('i686' 'x86_64')
license=('APACHE')
url="http://ant.apache.org/"
depends=('java-runtime')
optdepends=('junit: to jave junit on the classpath in javac tasks')
source=(http://archive.apache.org/dist/ant/binaries/${pkgname}-${pkgver}-bin.tar.bz2
        ${pkgname}.sh)
md5sums=('9330447f3763b87570dd1118c49a8efd'
         '593ee6ebd9b8ec321534a028e686880f')

build() {
  # install profile.d script
  install -dm755 ${pkgdir}/etc/profile.d || return 1
  install -dm755 ${pkgdir}/usr/share/java/${pkgname}-${pkgver}/ || return 1
  install -m755 ${srcdir}/${pkgname}.sh ${pkgdir}/usr/share/java/${pkgname}-${pkgver}/ || return 1

  # Get the ANT_HOME env var
#  source ${srcdir}/${pkgname}.sh || return 1

  cd ${srcdir}/${pkgname}-${pkgver}
  install -dm755 ${pkgdir}/usr/share/java/${pkgname}-${pkgver}/{bin,lib} || return 1

  install -m644 ./lib/*.jar ${pkgdir}/usr/share/java/${pkgname}-${pkgver}/lib || return 1
  cp -Rp ./etc ${pkgdir}/usr/share/java/${pkgname}-${pkgver} || return 1

  # Do not copy Windows .bat/.cmd files
  find ./bin -type f -a ! -name \*.bat -a ! -name \*.cmd \
    -exec install -m755 {} ${pkgdir}/usr/share/java/${pkgname}-${pkgver}/bin \; || return 1

  # symlink to junit so it's on the javac build path for ant
  # matches behavior on ubuntu 9 and makes sense for compatibility
  # http://bugs.archlinux.org/task/15229
  cd $startdir/pkg/usr/share/java/${pkgname}-${pkgver}/lib || return 1
  ln -s ../../junit.jar . || return 1
  cd - || return 1

  # The license says the NOTICE file should be redistributed for derivative
  # works, so lets supply it.
  install -dm755 ${pkgdir}/usr/share/licenses/${pkgname}-${pkgver} || return 1
  install -m644 LICENSE NOTICE ${pkgdir}/usr/share/licenses/${pkgname}-${pkgver} || return 1
}

