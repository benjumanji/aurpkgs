# Contributor: Hugo Doria <hugo@archlinux.org>
# Contributor: Guillaume ALAUX <guillaume at alaux dot net>
pkgname=tomcat
pkgver=5.5.30
pkgrel=1
arch=('any')
pkgdesc="Servlet container for Java Servlet and JavaServer Pages"
url="http://tomcat.apache.org/"
license=('APACHE')
depends=('java-environment')
makedepends=('docbook2x')
backup=('etc/tomcat/server.xml'
	'etc/tomcat/tomcat-users.xml'
	'etc/conf.d/tomcat')
optdepends='tomcat-admin'
install=tomcat.install
source=(http://archive.apache.org/dist/tomcat/tomcat-5/v${pkgver}/bin/apache-tomcat-${pkgver}.tar.gz
	'tomcat'
	'tomcat.conf.d')
md5sums=('661dd3fbc25249e9ceb86652eb6a4f07'
         'bdb5d655c5e20301a07106ab59d2e0b3'
         'dc7ba215d4629003cbd229b526d08501')

build() {
	unset LDFLAGS
	# build jsvc
	cd ${srcdir}/apache-${pkgname}-${pkgver}/bin
	tar xzf commons-daemon-native.tar.gz
	cd commons-daemon-1.0.2-native-src/unix
	sh configure
	make clean
	make || return 1
	cp jsvc ../..
	docbook2man man/jsvc.1.xml
}

package() {
	cd ${srcdir}/apache-${pkgname}-${pkgver}

	mkdir -p ${pkgdir}/usr/share/${pkgname} ${pkgdir}/srv/http/${pkgname} \
		${pkgdir}/var/log/${pkgname} ${pkgdir}/etc/${pkgname} \
		${pkgdir}/usr/share/man/man1

	mv bin/commons-daemon-1.0.2-native-src/unix/JSVC.1 \
		${pkgdir}/usr/share/man/man1
	
	# get rid of some cruft
	rm -f LICENSE NOTICE RELEASE-NOTES RUNNING.txt
	rm -fr  bin/*.exe bin/*.bat bin/*.tar.gz bin/*64/*.exe bin/jsvc-src logs \
		bin/commons-daemon-1.0.2-native-src

	# install everything
	mv ${srcdir}/apache-${pkgname}-${pkgver}/webapps ${pkgdir}/srv/http/${pkgname}
	ln -s /srv/http/${pkgname}/webapps ${pkgdir}/usr/share/${pkgname}/webapps

	sed -i 's|<Host name="localhost" appBase="webapps"|<Host name="localhost" appBase="/srv/http/tomcat/webapps"|g' \
		${srcdir}/apache-${pkgname}-${pkgver}/conf/server.xml
	mv ${srcdir}/apache-${pkgname}-${pkgver}/conf/* ${pkgdir}/etc/tomcat
	rmdir ${srcdir}/apache-${pkgname}-${pkgver}/conf
	ln -s /etc/tomcat ${pkgdir}/usr/share/${pkgname}/conf
	
	cp -R * ${pkgdir}/usr/share/${pkgname}

	install -D -m755 ${srcdir}/tomcat ${pkgdir}/etc/rc.d/tomcat
	install -D -m644 ${srcdir}/tomcat.conf.d ${pkgdir}/etc/conf.d/tomcat

	ln -s /var/log/${pkgname} ${pkgdir}/usr/share/${pkgname}/logs

	chmod 0660 ${pkgdir}/etc/tomcat/{*.xml,*.policy,*.properties,/Catalina/localhost/*.xml}
	chmod 0775 ${pkgdir}/etc/tomcat ${pkgdir}/usr/share/tomcat/work
}
