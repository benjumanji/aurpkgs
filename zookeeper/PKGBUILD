# Maintainer: Ben Alex <ben.alex@acegi.com.au>
# Contributor: Scott Lawrence <bytbox@gmail.com>
# Contributor: Guillaume ALAUX <guillaume at alaux dot net>
pkgname=zookeeper
pkgver=3.4.6
pkgrel=4
pkgdesc='Open-source server which enables highly reliable distributed coordination'
arch=('any')
url='https://zookeeper.apache.org/'
license=('Apache')
depends=('java-runtime')
backup=(etc/zookeeper/configuration.xsl
        etc/zookeeper/log4j.properties
        etc/zookeeper/zoo.cfg)
install=install_zookeeper.sh

source=(https://dist.apache.org/repos/dist/release/${pkgname}/${pkgname}-${pkgver}/${pkgname}-${pkgver}.tar.gz
        001_zkServer-clean-kill.patch
        systemd_zookeeper.service
        systemd_zookeeper@.service
        systemd_sysusers.d_zookeeper.conf
        systemd_tmpfiles.d_zookeeper.conf)

sha256sums=('01b3938547cd620dc4c93efe07c0360411f4a66962a70500b163b59014046994'
            '8c46c005579171988746197fe7c35061c007387d474fb8d05ecc74b37387634f'
            '78ab3546f895c41ee78b5446cfd29ab1f8722ab507d83e0e245918e840935660'
            '145809ca7b5941d462d785c9be833b57b9e9cc257b197faf0a38ad5f8fe5e243'
            '150c4df043553df3e9118496ae01eb9290d893b4cf7c68a667820b3e708379a1'
            '65cce4d87a98eff40bfbf2397db1e6f1405029163367f2bef0c142301cc982a8')

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  _app_home=/usr/share/${pkgname}

  install -d "${pkgdir}"{${_app_home}/bin,/etc,/usr/bin,/usr/share/{doc,java}}

  cp -r conf ${pkgdir}/etc/${pkgname}/
  ln -s /etc/${pkgname} "${pkgdir}${_app_home}/conf"

  patch -p0 < "${srcdir}/001_zkServer-clean-kill.patch"
  cp -r bin/*.sh  "${pkgdir}${_app_home}/bin/"
  sed -i "s|^ZOOBIN=\"\$(dirname \"\${ZOOBIN}\")\"|ZOOBIN=\"${_app_home}/bin\"|" \
    "${pkgdir}"${_app_home}/bin/*
  for b in zkCleanup.sh zkCli.sh zkServer.sh; do
    bname=$(basename $b)
    ln -s ${_app_home}/bin/${bname} "${pkgdir}/usr/bin/${bname}"
  done

  #cp -r docs "${pkgdir}/usr/share/doc/${pkgname}/"
  #ln -s /usr/share/doc/${pkgname} "${pkgdir}${_app_home}/docs"
  #cp -r src "${pkgdir}/usr/share/${pkgname}"

  cp -r lib "${pkgdir}/usr/share/java/${pkgname}"
  ln -s ../java/${pkgname} "${pkgdir}${_app_home}/lib"

  cp -r recipes "${pkgdir}/usr/share/${pkgname}"
  install -m 644 ${pkgname}-${pkgver}.jar \
    "${pkgdir}/usr/share/java/${pkgname}/${pkgname}-${pkgver}.jar"
  ln -s ${pkgname}-${pkgver}.jar \
    "${pkgdir}/usr/share/java/${pkgname}/${pkgname}.jar"
  ln -s lib/${pkgname}-${pkgver}.jar \
    "${pkgdir}/usr/share/${pkgname}/${pkgname}-${pkgver}.jar"

  install -D -m 644 "${srcdir}/systemd_zookeeper.service" \
    "${pkgdir}/usr/lib/systemd/system/zookeeper.service"
  install -D -m 644 "${srcdir}/systemd_zookeeper@.service" \
    "${pkgdir}/usr/lib/systemd/system/zookeeper@.service"
  install -D "${srcdir}/systemd_sysusers.d_zookeeper.conf" \
    "${pkgdir}/usr/lib/sysusers.d/${pkgname}.conf"
  sed "s|^dataDir=/tmp/zookeeper$|dataDir=/var/lib/${pkgname}|" \
    "${pkgdir}/etc/${pkgname}/zoo_sample.cfg" \
    > "${pkgdir}/etc/${pkgname}/zoo.cfg"
  install -D -m 644 "${srcdir}/systemd_tmpfiles.d_zookeeper.conf" \
    "${pkgdir}/usr/lib/tmpfiles.d/zookeeper.conf"
}
