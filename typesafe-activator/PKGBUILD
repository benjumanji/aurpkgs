# Maintainer: Guillaume ALAUX <guillaume at alaux dot net>
pkgname=typesafe-activator
pkgver=1.2.10
pkgrel=1
pkgdesc='Helper tools to get you started with Play and the Typesafe Reactive Platform - aka playframework'
arch=('any')
url='http://www.playframework.org/'
license=('custom')
depends=('java-environment' 'python')
conflicts=('playframework' 'playframework2')
replaces=('playframework' 'playframework2')
source=(http://downloads.typesafe.com/${pkgname}/${pkgver}/${pkgname}-${pkgver}.zip)
sha256sums=('7d721b93b21463ecdab12ec7b032896d7a0f0405636b113fc687a7ca2016ac3d')

package() {
  cd "${srcdir}/activator-${pkgver}"

  install -d -m 755 "${pkgdir}/usr/share/${pkgname}"
  cp -r templates repository activator-launch-${pkgver}.jar activator README.html \
    "${pkgdir}/usr/share/${pkgname}"
  chmod +x "${pkgdir}/usr/share/${pkgname}/activator"

  install -d -m 755 "${pkgdir}/usr/bin/"
  ln -s /usr/share/typesafe-activator/activator "${pkgdir}/usr/bin/activator"

  install -D -m 644 LICENSE.html "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.html"
}
